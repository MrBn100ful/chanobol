buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

def versionMajor = 2
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply plugin: 'com.android.application'

if (project.file('crashlytics.properties').exists()) {
    apply plugin: 'io.fabric'
}

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

dependencies {
    compile  'com.android.support:support-v4:22.2.+'
    compile  'com.android.support:appcompat-v7:22.2.+'
    compile  'com.android.support:cardview-v7:22.2.+'
    compile  'com.android.support:recyclerview-v7:22.2.+'
    compile  'com.android.support:gridlayout-v7:22.1.+'
    compile  'com.android.support:palette-v7:22.1.+'
    compile  'com.nineoldandroids:library:2.4.0'

    compile  'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    compile  'com.jakewharton:butterknife:6.0.0'
    compile  'com.jakewharton.timber:timber:2.5.0'
    compile  'org.parceler:parceler-api:0.2.15'
    provided 'org.parceler:parceler:0.2.15'

    compile 'org.jsoup:jsoup:1.7.3'

    compile  'com.koushikdutta.ion:ion:2.1.3'

    compile  'com.github.chrisbanes.photoview:library:1.2.2'

    compile project(':libs:fenster')

    compile ('com.crashlytics.sdk.android:crashlytics:2.2.0')


}

android {
    compileSdkVersion 26
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "mrbn100ful.chanobol"
        minSdkVersion 20
        targetSdkVersion 26

        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        versionCode versionMajor * 100000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = false
            }
        }
    }

    File f = file('keys.properties')
    boolean doSign = f.exists();

    if (doSign) {
        Properties props = new Properties()
        props.load(new FileInputStream(f))

        //noinspection GroovyAssignabilityCheck
        signingConfigs {
            release {
                storeFile file(props['keystoreFile'])
                storePassword props['keystorePass']
                keyAlias props['keyAlias']
                keyPassword props['keyPass']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
        release {

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (doSign) {
                signingConfig signingConfigs.release
            }
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}
